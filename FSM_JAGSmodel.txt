model {

# FSM model
# Cite:
# Mistry, P. K., Warren, S., Branigan, N. K., Cai, W., & Menon, V. (2024). Computational Modeling of Proactive, Reactive, and Attentional Dynamics in Cognitive Control. bioRxiv, 2024-10.
# Mistry, P. K., Branigan, N. K., Gao, Z., Cai, W., & Menon, V. (2024). Nonergodicity and Simpsonâ€™s paradox in neurocognitive dynamics of cognitive control. bioRxiv, 2024-07.

#--------- Go Process -----------#
    deltaG    ~ dunif(0.0001,12) 
    alphaG[1] ~ dunif(0.5,4) 
    alphaG[2] <- alphaG[1] 
    betaG     ~ dbeta(1,1) T(0.0001,0.9999)
    xMin      ~ dunif(0.0001,minT-0.01) 
    for (rr in 1:2) {
    for (t in 1:NN) {
        goProc[t,rr]  ~ dwiener(alphaG[1],xMin,betaG,delta[t,rr]) 
        delta[t,rr]   <- deltaG*stimLR[t,rr]       
        onest[t,rr]   ~ dinterval(equals(tr[t,rr],1)*maxtime+equals(tr[t,rr],2)*ifelse(stProc[t,rr]>=0,min(maxtime,stProc[t,rr]),maxtime)+equals(tr[t,rr],3)*abs(goProc[t,rr])+equals(tr[t,rr],4)*abs(goProc[t,rr]),equals(tr[t,rr],1)*abs(goProc[t,rr])+equals(tr[t,rr],2)*abs(goProc[t,rr])+equals(tr[t,rr],3)*maxtime+equals(tr[t,rr],4)*ifelse(stProc[t,rr]>=0,min(maxtime,stProc[t,rr]),maxtime))
        ones[t,rr]    ~ dbern(onest[t,rr]*0.999+(1-onest[t,rr])*0.001)
    }
    }

#--------- Stop Process -----------#
    pp     ~ dunif(0.01,1)
    for (rr in 1:2) {
    for (t in 1:NN) {
        stProc[t,rr] <- pp + ssdT[t,rr]
    }
    }

#------------------------Posterior Predictive------------------------
    for (rr in 1:2) {
    for (t in 1:NN) {
        pgoProc[t,rr]  ~ dwiener(alphaG[1],xMin,betaG,delta[t,rr]) 
        pstProc[t,rr]  <- pp
        pones[t,rr]    ~ dinterval(equals(tr[t,rr],1)*maxtime+equals(tr[t,rr],2)*ifelse(pstProc[t,rr]>=0,min(maxtime,pstProc[t,rr]),maxtime)+equals(tr[t,rr],3)*abs(pgoProc[t,rr])+equals(tr[t,rr],4)*abs(pgoProc[t,rr]),equals(tr[t,rr],1)*abs(pgoProc[t,rr])+equals(tr[t,rr],2)*abs(pgoProc[t,rr])+equals(tr[t,rr],3)*maxtime+equals(tr[t,rr],4)*ifelse(pstProc[t,rr]>=0,min(maxtime,pstProc[t,rr]),maxtime))
    }
    }
}